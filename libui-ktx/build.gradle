buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${rootProject.dokkaVersion}"
    }
}

apply plugin: 'org.jetbrains.dokka'

dokka {
    outputFormat = 'gfm'
    outputDirectory = rootProject.file('docs').absolutePath
    sourceRoot { path = 'libui-ktx/src/main/kotlin' }
    sourceRoot { path = 'libui-ktx/staging' }
    reportUndocumented = false
    linkMapping {
        dir = "src/main/kotlin"
        url = "https://github.com/msink/kotlin-libui/blob/master/${project.name}/src/main/kotlin"
        suffix = "#L"
    }
}

apply plugin: 'konan'

def konanUserDir = System.getenv("KONAN_DATA_DIR") ?: "${System.getProperty("user.home")}/.konan"
def windresDir = "$konanUserDir/dependencies/msys2-mingw-w64-x86_64-gcc-7.2.0-clang-llvm-5.0.0-windows-x86-64/bin"
def rcFile = file('src/test/resources/test.rc')
def resFile = file("$buildDir/konan/resources/test.res")
task windowsResources(type: Exec) {
    inputs.file rcFile
    outputs.file resFile
    commandLine "$windresDir/windres", rcFile, '-O', 'coff', '-o', resFile
    environment 'PATH', "$windresDir;${System.getenv('PATH')}"
}

konanArtifacts {
    interop('libui') {
        includeDirs "${project.rootDir}/libui/src"
    }

    library('libui-ktx') {
        libraries {
            artifact 'libui'
        }
    }

    program('test') {
        srcDir 'src/test/kotlin'
        libraries {
            artifact 'libui-ktx'
        }
        target 'mingw', {
            dependsOn 'windowsResources'
            inputs.file resFile
            linkerOpts "$resFile"
        }
    }
}
