// Top-level build file where you can add configuration options common to all sub-projects/modules.

import static java.nio.file.Files.find
import static java.nio.file.Paths.get
import static java.util.stream.Collectors.toSet

buildscript {
    ext.libuiVersion = 'alpha4.1'
    ext.kotlinVersion = '1.3.0'
    ext.dokkaVersion = '0.9.17'
    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://dl.bintray.com/jetbrains/kotlin-native-dependencies" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:${kotlinVersion}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokkaVersion}"
    }
}

allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }
}

apply plugin: 'org.jetbrains.dokka'

dokka {
    outputFormat = 'gfm'
    outputDirectory = 'docs'
    moduleName = ''

    def cinteropBuildDirs = find(get("${rootDir}/libui/build/classes/kotlin"), 16) {
        path, attributes -> path.endsWith("libui-cinterop-libui.klib-build")
    }.map({ it.toString() }).collect(toSet())

    sourceDirs = files(cinteropBuildDirs) +
            files(
                    "${rootDir}/staging",
                    "${rootDir}/libui/src/nativeMain/kotlin"
            )
    
    reportUndocumented = false
    noStdlibLink = true
    
    packageOptions {
        prefix = "kotlin"
        suppress = true
    }
    
    packageOptions {
        prefix = "kotlinx"
        suppress = true
    }
    
    packageOptions {
        prefix = "platform"
        suppress = true
    }
}
