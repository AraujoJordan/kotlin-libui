apply plugin: 'konan'

def konanUserDir = System.getenv("KONAN_DATA_DIR") ?: "${System.getProperty("user.home")}/.konan"
def windresDir = "$konanUserDir/dependencies/msys2-mingw-w64-x86_64-gcc-7.2.0-clang-llvm-5.0.0-windows-x86-64/bin"
def resFile = file("$buildDir/konan/resources/samples.res")
def rcFile = file('src/main/resources/samples.rc')

task windowsResources(type: Exec) {
    def path = System.getenv("PATH")

    commandLine "$windresDir/windres", rcFile, '-O', 'coff', '-o', resFile
    environment 'PATH', "$windresDir;$path"

    inputs.file rcFile
    outputs.file resFile
}

konanArtifacts {
    program('hello2') {
        libraries {
            artifact project(':libui-ktx'), 'libui-ktx'
        }
        target 'mingw', {
            linkerOpts "$resFile -mwindows"
            dependsOn 'windowsResources'
            inputs.file resFile
        }
    }
}
