apply plugin: 'konan'

def konanUserDir = System.getenv("KONAN_DATA_DIR") ?: "${System.getProperty("user.home")}/.konan"
def windresDir = "$konanUserDir/dependencies/msys2-mingw-w64-x86_64-gcc-7.2.0-clang-llvm-5.0.0-windows-x86-64/bin"
def rcFile = file('src/main/resources/samples.rc')
def resFile = file("$buildDir/konan/resources/samples.res")
task windowsResources(type: Exec) {
    inputs.file rcFile
    outputs.file resFile
    commandLine "$windresDir/windres", rcFile, '-O', 'coff', '-o', resFile
    environment 'PATH', "$windresDir;${System.getenv('PATH')}"
}

konanArtifacts {
    program('table') {
        srcDir 'src/main/kotlin'
        srcDir 'src/main/resources'
        libraries {
            artifact project(':libui-ktx'), 'libui-ktx'
        }
        target('mingw') {
            dependsOn 'windowsResources'
            inputs.file resFile
            linkerOpts "$resFile -mwindows"
        }
    }
}
